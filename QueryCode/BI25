// Q25. Weighted interaction paths
PROFILE
MATCH
  path=allShortestPaths((p1:Person {id: 19791209303405})-[:KNOWS*]-(p2:Person {id: 19791209308983}))
UNWIND relationships(path) AS k
WITH
  path,
  startNode(k) AS pA,
  endNode(k) AS pB,
  0 AS relationshipWeights

// case 1, A to B
// every reply (by one of the Persons) to a Post (by the other Person): 1.0
OPTIONAL MATCH
  (pA)<-[:HAS_CREATOR]-(c:Comment)-[:REPLY_OF]->(post:Post)-[:HAS_CREATOR]->(pB),
  (post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
WITH path, pA, pB, relationshipWeights + count(c)*1.0 AS relationshipWeights

// case 2, A to B
// every reply (by ones of the Persons) to a Comment (by the other Person): 0.5
OPTIONAL MATCH
  (pA)<-[:HAS_CREATOR]-(c1:Comment)-[:REPLY_OF]->(c2:Comment)-[:HAS_CREATOR]->(pB),
  (c2)-[:REPLY_OF*]->(:Post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
WITH path, pA, pB, relationshipWeights + count(c1)*0.5 AS relationshipWeights

// case 1, B to A
// every reply (by one of the Persons) to a Post (by the other Person): 1.0
OPTIONAL MATCH
  (pB)<-[:HAS_CREATOR]-(c:Comment)-[:REPLY_OF]->(post:Post)-[:HAS_CREATOR]->(pA),
  (post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
WITH path, pA, pB, relationshipWeights + count(c)*1.0 AS relationshipWeights

// case 2, B to A
// every reply (by ones of the Persons) to a Comment (by the other Person): 0.5
OPTIONAL MATCH
  (pB)<-[:HAS_CREATOR]-(c1:Comment)-[:REPLY_OF]->(c2:Comment)-[:HAS_CREATOR]->(pA),
  (c2)-[:REPLY_OF*]->(:Post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
WITH path, pA, pB, relationshipWeights + count(c1)*0.5 AS relationshipWeights
WITH
  [person IN nodes(path) | person.id] AS personIds,
  sum(relationshipWeights) AS weight
RETURN
  personIds,
  weight
ORDER BY
  weight DESC,
  personIds ASC

SOLUCION

MATERIALIZACION DE CAMINOS

MATCH
  (pA)<-[:HAS_CREATOR]-(c:Comment)-[:REPLY_OF]->(post:Post)-[:HAS_CREATOR]->(pB),
  (post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
create (pA)-[:BI25_reply_post]->(pB);

MATCH
  (pA)<-[:HAS_CREATOR]-(c1:Comment)-[:REPLY_OF]->(c2:Comment)-[:HAS_CREATOR]->(pB),
  (c2)-[:REPLY_OF*]->(:Post)<-[:CONTAINER_OF]-(forum:Forum)
WHERE forum.creationDate >= 20101031230000000 AND forum.creationDate <= 20101130230000000
create (pA)<-[:BI25_reply_comment]-(pB); 

// Q25. Weighted interaction paths
PROFILE
MATCH
  path=allShortestPaths((p1:Person {id: 19791209303405})-[:KNOWS*]-(p2:Person {id: 19791209308983}))
UNWIND relationships(path) AS k
WITH
  path,
  startNode(k) AS pA,
  endNode(k) AS pB,
  0 AS relationshipWeights

// case 1, A to B
// every reply (by one of the Persons) to a Post (by the other Person): 1.0
OPTIONAL MATCH
  (pA)-[c:BI25_reply_post]->(pB)
WITH path, pA, pB, relationshipWeights + count(c)*1.0 AS relationshipWeights

// case 2, A to B
// every reply (by ones of the Persons) to a Comment (by the other Person): 0.5
OPTIONAL MATCH
  (pA)<-[c1:BI25_reply_comment]-(pB)
WITH path, pA, pB, relationshipWeights + count(c1)*0.5 AS relationshipWeights

// case 1, B to A
// every reply (by one of the Persons) to a Post (by the other Person): 1.0
OPTIONAL MATCH
  (pB)-[c:BI25_reply_post]->(pA)
WITH path, pA, pB, relationshipWeights + count(c)*1.0 AS relationshipWeights

// case 2, B to A
// every reply (by ones of the Persons) to a Comment (by the other Person): 0.5
OPTIONAL MATCH
  (pB)<-[c1:BI25_reply_comment]-(pA)
WITH path, pA, pB, relationshipWeights + count(c1)*0.5 AS relationshipWeights
WITH
  [person IN nodes(path) | person.id] AS personIds,
  sum(relationshipWeights) AS weight
RETURN
  personIds,
  weight
ORDER BY
  weight DESC,
  personIds ASC
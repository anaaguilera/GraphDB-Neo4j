// Q21. Zombies in a country
PROFILE
MATCH (country:Country {name: 'Ethiopia'})
WITH
  country,
  20130101000000000/10000000000000   AS endDateYear,
  20130101000000000/100000000000%100 AS endDateMonth
MATCH
  (country)<-[:IS_PART_OF]-(:City)<-[:IS_LOCATED_IN]-(zombie:Person)
OPTIONAL MATCH
  (zombie)<-[:HAS_CREATOR]-(message:Message)
WHERE zombie.creationDate  < 20130101000000000
  AND message.creationDate < 20130101000000000
WITH
  country,
  zombie,
  endDateYear,
  endDateMonth,
  zombie.creationDate/10000000000000   AS zombieCreationYear,
  zombie.creationDate/100000000000%100 AS zombieCreationMonth,
  count(message) AS messageCount
WITH
  country,
  zombie,
  12 * (endDateYear  - zombieCreationYear )
     + (endDateMonth - zombieCreationMonth)
     + 1 AS months,
  messageCount
WHERE messageCount / months < 1
WITH
  country,
  collect(zombie) AS zombies
UNWIND zombies AS zombie
OPTIONAL MATCH
  (zombie)<-[:HAS_CREATOR]-(message:Message)<-[:LIKES]-(likerZombie:Person)
WHERE likerZombie IN zombies
WITH
  zombie,
  count(likerZombie) AS zombieLikeCount
OPTIONAL MATCH
  (zombie)<-[:HAS_CREATOR]-(message:Message)<-[:LIKES]-(likerPerson:Person)
WHERE likerPerson.creationDate < 20130101000000000
WITH
  zombie,
  zombieLikeCount,
  count(likerPerson) AS totalLikeCount
RETURN
  zombie.id,
  zombieLikeCount,
  totalLikeCount,
  CASE totalLikeCount
    WHEN 0 THEN 0.0
    ELSE zombieLikeCount / toFloat(totalLikeCount)
  END AS zombieScore
ORDER BY
  zombieScore DESC,
  zombie.id ASC
LIMIT 100

SOLUCION

MATERIALIZACION DE CAMINOS

MATCH (country:Country {name: 'Ethiopia'})
MATCH
  (country)<-[:IS_PART_OF]-(:City)<-[:IS_LOCATED_IN]
  -(zombie:Person)<-[:HAS_CREATOR]-(message:Message)
WHERE zombie.creationDate  < 20130101000000000
  AND message.creationDate < 20130101000000000
create (message)-[:BI21_message_person]->(zombie);

// Q21. Zombies in a country
PROFILE
MATCH (country:Country {name: 'Ethiopia'})
MATCH
  (country)<-[:IS_PART_OF]-(:City)<-[:IS_LOCATED_IN]-(zombie:Person)
OPTIONAL MATCH
  (zombie)<-[:BI21_message_person]-(message:Message)
WITH
  country,
  zombie,
  count(message) AS messageCount
WITH
  country,
  zombie,
  12 * (20130101000000000/10000000000000   - zombie.creationDate/10000000000000 )
     + (20130101000000000/100000000000%100 - zombie.creationDate/100000000000%100)
     + 1 AS months,
  messageCount
WHERE messageCount / months < 1
WITH
  country,
  collect(zombie) AS zombies
UNWIND zombies AS zombie
OPTIONAL MATCH
  (zombie)<-[:HAS_CREATOR]-(message:Message)<-[:LIKES]-(likerZombie:Person)
WHERE likerZombie IN zombies
WITH
  zombie,
  count(likerZombie) AS zombieLikeCount
OPTIONAL MATCH
  (zombie)<-[:HAS_CREATOR]-(message:Message)<-[:LIKES]-(likerPerson:Person)
WHERE likerPerson.creationDate < 20130101000000000
WITH
  zombie,
  zombieLikeCount,
  count(likerPerson) AS totalLikeCount,
  CASE count(likerPerson) 
    WHEN 0 THEN 0.0
    ELSE zombieLikeCount / toFloat(count(likerPerson))
  END AS zombieScore
ORDER BY
  zombieScore DESC,
  zombie.id ASC
LIMIT 100
RETURN
  zombie.id,
  zombieLikeCount,
  totalLikeCount,
  zombieScore